Функция ФИО(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	
	//Проверка на наличие недопустимых символов, допустимы только буквы, дефисы и пробелы
	//В данной реализации допускаются только ФИО на кириллице
	//Не допускается апостроф, хотя в иностранных фамилиях может присутствовать в любой части ФИО
	RegExp.Pattern = "[А-Яа-яЁё\s\-]";
	Matches = RegExp.Execute(СтрокаДляПроверки);  
	Результат = Matches.Count;
	
	//Если в строке больше символов, чем допустимых, значит в ней присутствуют недопустимые символы
	//Самое короткое имя состоит из 2 символов, самая короткая фамилия - из 1 символа, отчество может отсутствовать у иностранцев
	//Итого включая пробел минимум 4 символа
	Если Результат < СтрДлина(СтрокаДляПроверки) Или Результат < 4 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Однако наличие нужных символов не гарантирует их правильного порядка
	//Имя, фамилия и отчество могут содержать буквы и дефисы, между ними может быть не менее 1 пробела	
	
	RegExp.Pattern = "(([А-Яа-яЁё]+(\-[А-Яа-яЁё]+)+)|([А-Яа-яЁё]+))(\s+(([А-Яа-яЁё]+(\-[А-Яа-яЁё]+)+)|([А-Яа-яЁё]+)))+"; 
	//Под словом подразумевается набор символов длиной 1 или выше
	//((слово (дефис слово)+) или слово) (пробел(ы), ((слово (дефис слово)+) или слово))+
	//Под частью имени подразумевается слово или слово-слово, в ФИО допустимо произвольное количество частей имени, разделённое пробелами, но не менее 2
	Matches = RegExp.Execute(СтрокаДляПроверки);  
	Результат = Matches.Count;
	
	//Если шаблон не найден, проверка выдаёт ошибку
	//Если шаблон найден, хотя бы часть строки корректно заполнена
	Если Результат = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция СНИЛС(СтрокаДляПроверки) Экспорт
	Если СтрДлина(СтрокаДляПроверки) <> 11 Тогда
		Возврат Ложь;
	Иначе		
		ГлавныеЦифры = Сред(СтрокаДляПроверки, 1, 9);
		КонтрольнаяСуммаСтрока = Прав(СтрокаДляПроверки, 2);
		КорректнаяКонтрольнаяСуммаСтрока = "";
		КонтрольнаяСумма = 0;
		
		Для Индекс = 1 По 9 Цикл
			Символ = Сред(ГлавныеЦифры, Индекс, 1);		
			КонтрольнаяСумма = КонтрольнаяСумма + Число(Символ) * (10 - Индекс);
		КонецЦикла;
		
		Если КонтрольнаяСумма < 100 Тогда
			КорректнаяКонтрольнаяСуммаСтрока = Строка(Цел(КонтрольнаяСумма / 10)) + Строка(КонтрольнаяСумма % 10);
		ИначеЕсли КонтрольнаяСумма = 100 Тогда
			КорректнаяКонтрольнаяСуммаСтрока = "00";
		Иначе
			ОстатокОтДеления = КонтрольнаяСумма % 101;
			
			Если ОстатокОтДеления = 100 Тогда
				КорректнаяКонтрольнаяСуммаСтрока = "00";
			Иначе
				КорректнаяКонтрольнаяСуммаСтрока = Строка(Цел(ОстатокОтДеления / 10)) + Строка(ОстатокОтДеления % 10);
			КонецЕсли;
		КонецЕсли;
		
		Если КонтрольнаяСуммаСтрока <> КорректнаяКонтрольнаяСуммаСтрока Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;	
	
	Возврат Истина;
КонецФункции

Функция ИНН(СтрокаДляПроверки, ЮрЛицо = Ложь) Экспорт
	//Для ИП и физических лиц длина ИНН равна 12, для юридических лиц 10
	Если (ЮрЛицо И СтрДлина(СтрокаДляПроверки) <> 10) Или (Не ЮрЛицо И СтрДлина(СтрокаДляПроверки) <> 12) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[0-9]";
	Matches = RegExp.Execute(СтрокаДляПроверки);  
	Результат = Matches.Count;
	
	Если СтрДлина(СтрокаДляПроверки) <> Результат Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция НаименованиеБензина(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[А-Яа-яЁёa-zA-Z\d\s\-]";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	//Сравнивается количество найденных разрешённых символов с длиной строки, чтобы выявить неразрешённые символы
	Если Результат < СтрДлина(СтрокаДляПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Под словом подразумевается последовательный набор букв (латицина и кириллица) или цифр длиной не менее 1 символа
	//Шаблон пропускает любой набор слов, написанных через дефис или пробел(ы), но не менее 1 слова
	RegExp.Pattern = "[А-Яа-яЁёa-zA-Z\d]+((\-|\s+)[А-Яа-яЁёa-zA-Z\d]+)*";
	Matches = RegExp.Execute(СтрокаДляПроверки);  
	Результат = Matches.Count;
	
	Если Результат <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция Наименование(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[А-Яа-яЁёa-zA-Z\d\s\-\""]";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	//Сравнивается количество найденных разрешённых символов с длиной строки, чтобы выявить неразрешённые символы
	Если Результат < СтрДлина(СтрокаДляПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПрисутствуютКавычки = Ложь;
	
	Если СтрНайти(СтрокаДляПроверки, "«") > 0 Тогда
		СтрокаДляПроверки = СтрЗаменить(СтрокаДляПроверки, "«", """");
		СтрокаДляПроверки = СтрЗаменить(СтрокаДляПроверки, "»", """");
		ПрисутствуютКавычки = Истина;
	ИначеЕсли СтрНайти(СтрокаДляПроверки, "'") > 0 Тогда
		СтрокаДляПроверки = СтрЗаменить(СтрокаДляПроверки, "'", """");
		ПрисутствуютКавычки = Истина;
	ИначеЕсли СтрНайти(СтрокаДляПроверки, """") > 0 Тогда
		ПрисутствуютКавычки = Истина;
	КонецЕсли;
	
	Если ПрисутствуютКавычки Тогда
		RegExp.Pattern = "[\""]";
		Matches = RegExp.Execute(СтрокаДляПроверки);  
		Результат = Matches.Count;
		
		//При наличии более 2 кавычек проверка выдаст ошибку
		Если Результат > 2 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		RegExp.Pattern = "\""[a-zA-Zа-яА-ЯёЁ]+[\s\-\da-zA-Zа-яА-ЯёЁ]*\""";
		Matches = RegExp.Execute(СтрокаДляПроверки);
		Результат = Matches.Count;
		
		//Проверяется, находится ли внутри кавычек хотя бы один буквенный символ, за которым могут следовать буквы, дефисы, пробелы и цифры
		Если Результат < 1 Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		RegExp.Pattern = "([a-zA-Zа-яА-ЯёЁ\d]+)((\s+|\-)[a-zA-Zа-яА-ЯёЁ\d]+)*";
		Matches = RegExp.Execute(СтрокаДляПроверки);
		Результат = Matches.Count;
		
		//Проверяется, что в строке присутствуют слова, разделённые 1 дефисом или пробелами
		Если Результат <> 1 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция Адрес(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[А-Яа-яЁё\d\s\-\.\,]";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	//Сравнивается количество найденных разрешённых символов с длиной строки, чтобы выявить неразрешённые символы
	Если Результат < СтрДлина(СтрокаДляПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	                          
	//В данной реализации другие проверки отсутствуют, так как адрес может принимать очень много различных форм
	Возврат Истина;
КонецФункции

Функция Телефон(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[\d\-\(\)\+\s]";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	//Сравнивается количество найденных разрешённых символов с длиной строки, чтобы выявить неразрешённые символы
	Если Результат < СтрДлина(СтрокаДляПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Шаблон ориентирован на российские мобильные и городские номера с кодом из 3 цифр
	RegExp.Pattern = "^((8|\+7)[\-\s]?)?(\(?\d{3}\)?[\-\s]?)?[\d\-\s]{7,10}$";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	Если Результат = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция ЭлПочта(СтрокаДляПроверки) Экспорт
	//Сначала убираются пробелы в начале и конце строки
	СтрокаДляПроверки = СокрЛП(СтрокаДляПроверки);
	RegExp = Новый COMОбъект("VBScript.RegExp");  
	RegExp.Global = Истина;
	RegExp.Pattern = "[a-zA-Z\d\-\.\_@]";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	//Сравнивается количество найденных разрешённых символов с длиной строки, чтобы выявить неразрешённые символы
	Если Результат < СтрДлина(СтрокаДляПроверки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	//Шаблон учитывает лишь наиболее стандартные электронные адреса и не включает все специальные символы, доступные в разных сервисах
	RegExp.Pattern = "[a-zA-Z\d]+((\.|\-|\_+)[a-zA-Z\d]+)*@[a-zA-Z\d]+((\.|\-|\_+)[a-zA-Z\d]+)+";
	Matches = RegExp.Execute(СтрокаДляПроверки);
	Результат = Matches.Count;
	
	Если Результат <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции
